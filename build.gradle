plugins {
  id 'groovy'
  id 'maven-publish'
  id 'eclipse'
  id 'idea'
  id 'com.readytalk.ci' version '0.2.0'
  id 'com.jfrog.artifactory' version '3.0.1'
}

repositories {
  jcenter()
}

dependencies {
  compile (
    'org.codehaus.groovy:groovy-all:2.1.0'
  )
  testCompile (
    'xmlunit:xmlunit:1.3',
    'junit:junit:4.11',
    'org.spockframework:spock-core:0.7-groovy-2.0'
  )
}

artifactoryPublish.onlyIf {
  System.env.'TRAVIS_BRANCH' == "master" && System.env.'TRAVIS_PULL_REQUEST' == "false"
}

artifactory {
  contextUrl = "http://oss.jfrog.org/artifactory"

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = System.env.BINTRAY_USER
      password = System.env.BINTRAY_API_KEY
      maven = true
    }
  }
}

defaultTasks 'build'

compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.2'
}
